// *If you want the envelop to enclose the peaks and troughs set Period to about 1.5 time the cycle peiod. 
// *If you want it to track the cycle set Period to about half the cycle period. 
// *NumATRs sets the channel width. 
 
inputs:   
	Price( (H+L)/2 ),  
	Period(100),  
	NumATRs( 1.5 ),  
	ATR_Lth(90),  
	Color(Yellow); 
 
variables:   
	Avg( 0 ),  
	Shift( 0 ),  
	LowerBand( 0 ),  
	UpperBand( 0 ),  
	a1(0), 
	b1(0), 
	coef1(0), 
	coef2(0), 
	coef3(0), 
	Butter(0), 
	Lag( 2 * Period / (3.14159 * 3.14159) ); 
 
a1 = expvalue(-1.414 * 3.14159 / Period); 
b1 = 2 * a1 * Cosine(1.414 * 180 / Period); 
coef2 = b1; 
coef3 = -a1 * a1; 
coef1 = (1 - b1 + a1 * a1) / 4; 
 
Butter = coef1 * (Price + 2 * Price[1] + Price[3]) + coef2 * Butter[1] + coef3 * Butter[2]; 
 
if currentbar < 3 then  
	Butter = Price; 
 
Shift = NumATRs * AvgTrueRange( ATR_Lth ) ; 
UpperBand = Butter + Shift ; 
LowerBand = Butter - Shift ; 
 
if lag > 0 or CurrentBar > AbsValue( lag ) then begin 
 
	Plot1[lag]( UpperBand, "UpperBand", Color ) ; 
	Plot2[lag]( LowerBand, "LowerBand", Color ) ; 
	Plot3[lag]( Butter, "MidLine", Color, Default,2 ) ; 
 
	{ Alert criteria } 
	if lag <= 0 then begin 
	 
		if Price crosses over UpperBand then 
			Alert( "Price crossing over upper band" )  
		else if Price crosses under LowerBand then 
			Alert( "Price crossing under lower band" ) ; 
			 
	end ; 
		 
end ;  

inputs:		LPPeriod(30), 
			K1(.85), 
			K2(.4);

variables:	alpha1(0), 
			HP(0), 
			a1(0), 
			b1(0), 
			c1(0), c2(0), c3(0), 
			Filt(0), 
			Peak(0), 
			X(0), 
			Quotient1(0), Quotient2(0), Quotient3(0), Quotient4(0),
			NegativeK1(0), NegativeK2 (0);

//Highpass filter cyclic components
//whose periods are shorter than
//100 bars


alpha1 = (Cosine(.707 * 360 / 100) + Sine(.707 * 360 / 100) - 1 ) /
			Cosine(.707 * 360 / 100);


HP = (1 - alpha1 / 2) * (1 - alpha1 / 2) * 
		(Close - 2 * Close[1] + Close[2]) + 2 * (1 - alpha1) * HP[1] 
		- (1 - alpha1) * (1 - alpha1) * HP[2];

//SuperSmoother Filter
a1 = expvalue( -1.414 * 3.14159 / LPPeriod ) ;
b1 = 2 * a1 * Cosine( 1.414*180 / LPPeriod ) ;
c2 = b1 ;
c3 = -a1 * a1 ;
c1 = 1 - c2 - c3 ;
Filt = c1 * ( HP + HP[1] ) / 2 + c2 * Filt[1]
+ c3 * Filt[2] ;

//Fast Attack - Slow Decay Algorithm
Peak = .991 * Peak[1] ;
If AbsValue( Filt ) > Peak then 
Peak = AbsValue( Filt ) ;

//Normalized Roofing Filter
if Peak <> 0 then 
    X = Filt / Peak ;
    
// Make K negative    
NegativeK1=-1 * K1; NegativeK2 = -1 * K2;    

Quotient1 = ( X + K1 ) / ( K1 * X + 1 ) ;
Quotient2 = ( X + K2 ) / ( K2 * X + 1 ) ;
Quotient3 = ( X + NegativeK1) / (NegativeK1 * X +1);
Quotient4 = ( X + NegativeK2) / (NegativeK2 * X +1);

Plot1( Quotient1, "Quotient1 UP",Green ) ;
Plot2( Quotient2, "Quotient2 UP",DarkGreen ) ;
PLot3 (Quotient3, "Quotient3 Down",Red );
Plot4 (Quotient4, "Quotient4 Down",DarkRed );


Plot5( 0, "ZL",DarkBlue ) ;  
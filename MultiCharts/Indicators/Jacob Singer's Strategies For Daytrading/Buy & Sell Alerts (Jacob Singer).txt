[LegacyColorValue = true]; 

{ !BuySell and Alerts
Jacob Singer, "DayTrading Strategies", TASC
Plots the author's BuySell & Alerts}

inputs:
	PerfLength( 104 ),
	VolLength( 25 ) ;

variables:
	BuySell( "" ),
	Perf( 0 ),
	VolAvg( 0 ),
	TrueClose( 0 ),
 	BSColor( Green ),
	PPColor( Green ),
	PivotPoint( 0 ),
	PivotPointMA3( 0 ),
	PivotPointMA5( 0 ),
	Alert1( "" ),
	Alert2( "" ),
	Alert3( "" ),
    	AlertString( "" );


{ Calculate 'True Close' }
if High[1] = Close[1] then
	TrueClose = MaxList( High[1], Open )
	else if Low[1] = Close[1] then
	TrueClose = MinList( Low[1], Open )
	else TrueClose = Close[1] ;


{ Calculate Buy/Sell Bias }
if Close - TrueClose > 0 then
	BuySell = "+1"
	else if Close - TrueClose < 0 then
	BuySell = "-1"
	else BuySell = "0" ;


{ Set Buy/Sell's Color ( Red or Green ) }
if BuySell = "+1" then BSColor = Green 
	else if BuySell = "-1" then	BSColor = Red
	else BSColor = Yellow ;

{ Calculate 'Watch' Alert }
VolAvg = AverageFC( Volume, VolLength ) ;

if ( Close[1] >= High[1] - .10 * Range[1] or
	Close[1] <= Low[1] + .10 * Range[1] ) and
	Volume[1] > (2 * VolAvg ) and 
    	Volume[1] < (4 * VolAvg ) then
	begin
	Alert( "Watch!" ) ;
	Alert1 = " Watch! " ;
	end
	else Alert1 = "" ;	

{ Calculate Pivot Point Alerts }
PivotPoint = ( High[1] + Low[1] + Close[1] ) / 3 ;
PivotPointMA3 = Average( PivotPoint, 3 ) ;
PivotPointMA5 = Average( PivotPoint, 5 ) ;

if Close[1] > PivotPoint and PivotPoint >
	PivotPointMA3 then
	begin
	Alert( "Bullish Setup" ) ;
	Alert2 = " Bullish " ;
	PPColor = Green ;
	end
	else Alert2 = "" ;

if Close[1] < PivotPoint and PivotPoint < 
PivotPointMA3 and PivotPoint <PivotPointMA5 then
	begin
	Alert( "Bearish Setup" ) ;
	Alert3 = " Bearish " ;
	PPColor = Red ;
	end
else Alert3 = "" ;

AlertString = Alert1 + Alert2 + Alert3 ;

Plot1( BuySell, "Buy/Sell", BSColor ) ;
Plot2( AlertString, "Alert Status", PPColor ) ;

